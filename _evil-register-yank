#!/usr/bin/env zsh

# Let handlers override normal [[:alnum:]_] registers
if (( ${+ZSH_EVIL_COPY_HANDLERS[$_evil_register]} )); then
	zle .vi-set-buffer x
	local x
	x="${registers[x]}"
	zle "$1"
	eval ${ZSH_EVIL_COPY_HANDLERS[$_evil_register]} <<< "${registers[x]}"
	registers[x]="$x"
	return
fi

case $_evil_register in

''|[0-9_]) zle "$1" ;;
[a-zA-Z]) # sync
	if ! typeset +f _evil-remote-set >/dev/null; then
		case $ZSH_EVIL_SYNC_EDITOR in
		nvim*|neovim*|nvr)
			(( $+commands[nvr] )) &&
				_evil-remote-set(){
					timeout 10 -k 15 nvr -s --nostart --remote-expr \
					"setreg('$1', \"${${registers[${1:l}]}//(#m)[\\\"]/\\$MATCH}\")" >/dev/null
				}
			;;
		(|g)vim*)
			# parse version to detect server support
			[[ $(vim --version) = *+clientserver* ]] &&
				_evil-remote-set(){
					timeout 10 -k 15 vim --servername "${VIMSERVER:="$ZSH_EVIL_SYNC_EDITOR"}" --remote-expr \
					"setreg('$1', \"${${registers[${1:l}]}//(#m)[\\\"]/\\$MATCH}\")" >/dev/null
				}
			;;
		*)
			# no match found, do default zle and exit
			zle "$1"
			unset _evil_register
			return
			;;
		esac

	fi
	# append or set remote
	zle "$1"
	# fork and run in subshell to supress job control messages
	( _evil-remote-set "$_evil_register" & )
;;
*) zle "$1" ;;

esac

unset _evil_register
