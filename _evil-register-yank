#!/usr/bin/env zsh

# Let handlers override normal [[:alnum:]_] registers
if (( ${+ZSH_EVIL_COPY_HANDLERS[$_evil_register]} )); then
	zle .vi-set-buffer x
	local x
	x="${registers[x]}"
	zle "$1"
	eval ${ZSH_EVIL_COPY_HANDLERS[$_evil_register]} <<< "${registers[x]}"
	registers[x]="$x"
	return
fi

case $_evil_register in

''|[0-9_]) zle "$1" ;;
[a-z]) # replace
	zle "$1"
	case $EVIL_SYNC_EDITOR in
		nvim*|neovim*|nvr)
			nvr -s --nostart --remote-silent -c "let @$_evil_register = \"$(sed "s/[\\\"']/\\\&/g")\""
		;;
		vim*)
			[[ $(vim --version) = *+clientserver* ]] &&
				vim --servername "${VIMSERVER:=${"$(vim --serverlist)"%%$'\n'*}}" --remote-send "let @$_evil_register = \"$(sed "s/[\\\"']/\\\&/g")\""
		;;
	esac
;;
[A-Z]) # append
	if ! typeset +f _evil-remote-append >/dev/null; then
		case $EVIL_SYNC_EDITOR in
		nvim*|neovim*|nvr)
			(( $+commands[nvr] )) &&
				_evil-remote-append(){
					nvr -s --nostart --remote-silent -c \
					"let @$_evil_register = @$_evil_register . \"$(sed "s/[\\\"']/\\\&/g")\""
				}
			;;
		vim*)
			# parse version to detect server support
			[[ $(vim --version) = *+clientserver* ]] &&
				_evil-remote-append(){
					vim --servername "${VIMSERVER:=${"$(vim --serverlist)"%%$'\n'*}}" --remote-silent -c \
					"let @$_evil_register = @$_evil_register . \"$(sed "s/[\\\"']/\\\&/g")\""
				}
			;;
		*)
			# no match found, do default zle and exit
			zle "$1"
			unset _evil_register
			return
			;;
		esac
	fi
	_evil_register="${_evil_register:l}"
	zle .vi-set-buffer "$_evil_register"
	local old="${registers[$_evil_register]}"
	zle "$1"
	# append to remote register
	_evil-remote-append <<< "${registers[$_evil_register]}"
	# append to zsh register
	registers[$vi_register]="$old${registers[$vi_register]}"
;;
*) zle "$1" ;;

esac

unset _evil_register
